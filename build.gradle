buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        maven {
            url = 'https://repo.spongepowered.org/repository/maven-public/'
            content { includeGroup "org.spongepowered" }
        }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
    }
}

plugins {
    id 'com.matthewprenger.cursegradle' version '1.4.0'
}

repositories {
    maven {
        url = "https://dvs1.progwml6.com/files/maven/"
    }
    maven {
        url "https://www.cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    maven {
        url = "https://maven.theillusivec4.top/"
    }
    maven {
        name = "ModMaven"
        url = "https://modmaven.dev"
    }
    maven { url "https://maven.shedaniel.me" }
    maven { url "https://maven.architectury.dev" }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'maven-publish'
apply plugin: 'org.spongepowered.mixin'

group = 'com.refinedmods'
archivesBaseName = 'refinedstorage'
version = '1.11.8'

if (System.getenv('GITHUB_SHA') != null) {
    version += '+' + System.getenv('GITHUB_SHA').substring(0, 7)
}

if (System.getenv('RELEASE') != null) {
    version = System.getenv('RELEASE').substring(1) // remove 'v'
}

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xmaxerrs" << "999"
}

mixin {
    add sourceSets.main, 'mixins.refinedstorage.refmap.json'
    config 'mixins.refinedstorage.json'
}

minecraft {
    mappings channel: 'official', version: '1.19.2'
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.markers', ''
            property 'forge.logging.console.level', 'debug'
            mods {
                refinedstorage {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            mods {
                refinedstorage {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            args '--mod', 'refinedstorage', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources')
            mods {
                refinedstorage {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets {
    main.resources.srcDirs += 'src/generated/resources'
}

processResources {
    inputs.property 'version', project.version
    filesMatching('META-INF/mods.toml') {
        expand 'version': project.version
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.19.2-43.4.0'
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'

    compileOnly fg.deobf("mezz.jei:jei-1.19.2-common-api:11.3.0.262")
    compileOnly fg.deobf("mezz.jei:jei-1.19.2-forge-api:11.3.0.262")
    runtimeOnly fg.deobf("mezz.jei:jei-1.19.2-forge:11.3.0.262")

    compileOnly fg.deobf('curse.maven:mouse-tweaks-60089:3871353')

    implementation fg.deobf("curse.maven:ic2-classic-242942:5230420")

    compileOnly fg.deobf('curse.maven:crafting-tweaks-233071:3914007')

    runtimeOnly fg.deobf("top.theillusivec4.curios:curios-forge:1.19.2-5.1.1.0")
    compileOnly fg.deobf("top.theillusivec4.curios:curios-forge:1.19.2-5.1.1.0:api")
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : "refinedstorage",
                "Specification-Vendor"    : "refinedmods",
                "Specification-Version"   : "1",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.jar.archiveVersion,
                "Implementation-Vendor"   : "refinedmods",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

if (System.getenv("CURSEFORGE_TOKEN") != null) {
    curseforge {
        apiKey = System.getenv("CURSEFORGE_TOKEN")
        project {
            id = '243076'
            changelog = System.getenv("CHANGELOG")
            changelogType = 'markdown'
            releaseType = project.version.toString().contains('beta') ? 'beta' : (project.version.toString().contains('alpha') ? 'alpha' : 'release')
            addGameVersion "1.19.2"
            mainArtifact(jar) {
                displayName = "v$project.version"
            }
        }
    }
}

jar.finalizedBy('reobfJar')

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/refinedmods/refinedstorage")
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
        maven {
            name = "CreeperHost"
            url = uri("https://maven.creeperhost.net/release")
            credentials {
                username = System.getenv("CREEPERHOST_MAVEN_USERNAME")
                password = System.getenv("CREEPERHOST_MAVEN_TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}

javadoc {
    failOnError = false
}
